apply plugin:'java'
apply plugin:'groovy'
apply plugin:'osgi'
apply plugin:'maven'

configurations {
    deployerJars
}

/*
  Repo/dependencies config
*/

repositories {
  mavenRepo urls: 'http://mirrors.ibiblio.org/pub/mirrors/maven2/'
  mavenRepo urls: 'http://repository.codehaus.org/'
  mavenRepo urls: 'http://download.java.net/maven/2/'
  mavenRepo urls: 'http://scala-tools.org/repo-releases/' 
  mavenRepo urls: 'http://jansi.fusesource.org/repo/release'
  mavenRepo urls: 'http://jline.sourceforge.net/m2repo'
  mavenRepo urls: 'http://www.aQute.biz/repo'
  mavenRepo urls: 'http://repository.jboss.org/maven2/'
  mavenRepo urls: 'http://repo.smokejumperit.com'
  mavenCentral()
}
dependencies {
	groovy "org.codehaus.groovy:groovy:$groovyVersion"

  deployerJars 'org.apache.maven.wagon:wagon-ssh:1.0-beta-2'

  compile "log4j:log4j:[1.2,1.3)"
  compile "junit:junit:[4,5)"

  testRuntime 'org.apache.ant:ant:[1.8,1.9)'

}

uploadArchives { 
  repositories.mavenDeployer {
    def repoUser = System.properties['sjit.repo.user']
    configuration = configurations.deployerJars
    repository(url: "scp://repo.smokejumperit.com/home/$repoUser/repo.smokejumperit.com/") {
      authentication(
        userName: repoUser,
        privateKey: new File(System.getProperty('sjit.repo.keyFile', '.'))?.absolutePath, 
        passphrase: System.properties['sjit.repo.passphrase']
      )   
      pom {
        groupId=mavenGroupId
        artifactId=mavenArtifactId
        version=version
      }
    }
  }
}

uploadArchives.dependsOn test

task taskdefGroovy << {
  ant.taskdef(
    name:"groovy",
    classname: "org.codehaus.groovy.ant.Groovy"
  )
}

task runExerciseScript(dependsOn:["jar", "taskdefGroovy"]) << {
  ant.groovy(
    fork:true,
    src:new File("$rootDir/test", "ExerciseScript.groovy").canonicalPath,
  ) {
    ant.classpath {
      pathelement(location:jar.archivePath.canonicalPath)
      [configurations.testRuntime].each { config ->
        config.resolve()
        config.files.each { file ->
          ant.pathelement(location:"$file")
        }
      }
      ant.pathelement(path:System.properties['java.class.path'])
    }
  }
}
